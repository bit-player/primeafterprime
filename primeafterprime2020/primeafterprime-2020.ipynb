{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Prime After Prime (2020 version)\n",
    "\n",
    "This notebook explores correlations among the residues modulo $m$ of consecutive prime numbers. The notebook is an accompaniment to the article \"[Prime After Prime](http://bit-player.org)\" published at bit-player.org on May 31, 2016. These correlations are *not* my discovery; they were first fully described and analyzed by Robert J. Lemke Oliver and Kannan Soundararajan in March 2016. For the mathematical background, see their arXiv preprint: [Unexpected Biases in the Distribution of Consecutive Primes](http://arxiv.org/abs/1603.03720). \n",
    "\n",
    "The original version of the notebook was written in version 0.4 of \n",
    "\n",
    "The code is written in the [Julia programming language](http://julialang.org/) within a [Jupyter notebook](http://jupyter.org/). The original version was written in Julia version 0.4, which is now very much out of date. In August of 2020, inspired by an inquiry from a high school student, I set about updating the code for use with Julia 1.0 or later. This was easier than I expected, partly because I discovered I was able to throw away much of the code. Functions I had written are available in Julia packages. Also, I have only included the essentials for exploring the mathematics and plotting results. I have eliminated various false starts and dead ends that serve mainly to distract.\n",
    "\n",
    "The 2016 version of the notebook, primeafterprime.ipynb, remains available in this repository."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Basic operating instructions\n",
    "\n",
    "Open this file in a Jupyter notebook or a Jupyter lab, with a kernel running Julia >= 1.0. (My testing was done with Julia 1.5.1.) Evaluate all the cells (there's a \"Run All\" command under the \"Run\" menu). The first time you do this, it make take several minutes to load and precompile the packages needed. Subsequent runs will be much faster.\n",
    "\n",
    "To test that the system is working, go to the last cell in the file, uncomment the lines there, and evaluate the cell. Again the first run may take a while, but you should eventually see a pink-and-blue heatmap like the ones in the bit-player article.\n",
    "\n",
    "For your own experiments, the protocol is as follows:\n",
    "\n",
    "Generate a list of consecutive primes. For example, get the first 10,000 primes larger than 500,000:\n",
    "\n",
    "```Julia\n",
    "p50000 = generate_primes(500000, 10000)\n",
    "```\n",
    "\n",
    "Now examine those primes modulo m and analyze correlations between successive residues mod m. For m = 7:\n",
    "\n",
    "```Julia\n",
    "p50000mod7 = analyze_primes_mod_m(p50000, 7)\n",
    "```\n",
    "\n",
    "The matrices p50000mod7[:nz_class_counts] and p50000mod7[:nz_pair_counts] are likely to be the main focus of interest.\n",
    "\n",
    "To produce a graphic heatmap showing the variations in the pair correlation function, do:\n",
    "\n",
    "```Julia\n",
    "hm = heatmap(p50000mod7)\n",
    "```\n",
    "\n",
    "(Or do something else with the data that you find interesting!)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Set up the Julia environment with all needed packages. \n",
    "\n",
    "# Evaluate this cell first! On first run, will generate a long scroll of terminal output,\n",
    "# and may take five or ten minutes.\n",
    "\n",
    "using Pkg\n",
    "Pkg.activate(@__DIR__)\n",
    "Pkg.instantiate()\n",
    "pkg\"precompile\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Primes"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate a list of 'n' primes starting with the first prime\n",
    "# greater than or equal to 'start'. The result is returned as an\n",
    "# array of integers of the same type as the type of 'start'.\n",
    "\n",
    "# CAUTION: If 'start' is just below a boundary between integer types,\n",
    "# this procedure may throw an OverflowError or, worse, return a\n",
    "# list of primes that wraps around to those at the origin of the \n",
    "# number line. The boundaries in question are:\n",
    "# typemax(Int64) = 9223372036854775807\n",
    "# typemax(Int128) = 170141183460469231731687303715884105727\n",
    "# If you are working in that neighborhood, it's best to explicitly\n",
    "# coerce 'start' to the larger type, i.e. Int128(9223372036854775807) or\n",
    "# BigInt(170141183460469231731687303715884105727)\n",
    "\n",
    "function generate_primes(start, n)\n",
    "    primelist = Array{typeof(start)}(undef, n)\n",
    "    p = nextprime(start)\n",
    "    for i = 1:n\n",
    "        primelist[i] = p\n",
    "        p = nextprime(p + 1)\n",
    "    end\n",
    "    return primelist\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Auxiliary to 'analyze_primes_mod_m'\n",
    "\n",
    "# Given a modulus m, return a vector of all possible\n",
    "# congruence classes for p mod m, where p is a prime > m.\n",
    "# When m is prime, the vector will consist of the range\n",
    "# 1:m-1, but for composite m there may be missing values.\n",
    "# For example, congruence_classes(10) = [1, 3, 7, 9]\n",
    "\n",
    "function collect_congruence_classes(m)\n",
    "    classes = Array{Int64}(undef, 0)\n",
    "    for i = 1:m\n",
    "        if gcd(i, m) == 1\n",
    "            push!(classes, i)\n",
    "        end\n",
    "    end\n",
    "    return classes\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Auxiliary to 'analyze_primes_mod_m' \n",
    "\n",
    "function nonzero_class_counts(class_counts, classes)\n",
    "    n = length(classes)\n",
    "    nz_counts = zeros(Int64, n)\n",
    "    for i = 1:n\n",
    "        nz_counts[i] = class_counts[classes[i] + 1]\n",
    "    end\n",
    "    return nz_counts\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Auxiliary to 'analyze_primes_mod_m'\n",
    "\n",
    "function nonzero_pair_counts(pair_counts, classes)\n",
    "    n = length(classes)\n",
    "    nz_counts = zeros(Int64, n, n)\n",
    "    for i = 1:n\n",
    "        for j = 1:n\n",
    "            nz_counts[i, j] = pair_counts[classes[i] + 1, classes[j] + 1]\n",
    "        end\n",
    "    end\n",
    "    return nz_counts\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Auxiliary to 'analyze_primes_mod_m'\n",
    "\n",
    "# calculate the mean value of all off-diagonal elements and the\n",
    "# mean of the on-diagonal elements; return the ratio of off/on\n",
    "\n",
    "function diagonal_ratio(count_matrix)\n",
    "    n = size(count_matrix, 1)\n",
    "    diagonal_sum = 0\n",
    "    for i = 1:n\n",
    "        diagonal_sum += count_matrix[i, i]\n",
    "    end\n",
    "    off_diagonal_sum = sum(count_matrix) - diagonal_sum\n",
    "    return (off_diagonal_sum / (n^2 - n)) / (diagonal_sum / n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "# given a list of primes and an integer modulus m,\n",
    "# returns a statistical analysis of the primes mod m, organized\n",
    "# as a data dictionary\n",
    "\n",
    "using Statistics\n",
    "\n",
    "function analyze_primes_mod_m(primelist, m)\n",
    "    \n",
    "    # associative structure to hold all data on primes\n",
    "    d = Dict()\n",
    "    \n",
    "    # basic info about the set of primes and the modulus\n",
    "    d[:first_prime] = primelist[1]\n",
    "    d[:last_prime] = primelist[end]\n",
    "    d[:median_prime] = primelist[div(end, 2)]\n",
    "    d[:prime_count] = length(primelist)    \n",
    "    d[:m] = m\n",
    "    \n",
    "    # set of integers that are possible residues for a prime p mod m\n",
    "    d[:congruence_classes] = collect_congruence_classes(m)\n",
    "    \n",
    "    # log10 of the median will become x coordinate in certain graphs\n",
    "    d[:magnitude] = Float64(log10(d[:median_prime]))\n",
    "    d[:digits] = round(Int, (ceil(d[:magnitude])))\n",
    "    \n",
    "    # arrays of zeros to hold counts of residue classes and consecutive pairs\n",
    "    class_counts = zeros(Int, m)\n",
    "    pair_counts = zeros(Int, (m, m))\n",
    "    \n",
    "    # loop through the primes mod m, recording the number in\n",
    "    # each congruence class and the number of each p_{i}, p_{i+1} \n",
    "    # pair of successive primes; adjust indices to translate\n",
    "    # from 0:m-1 range of modular values to 1:m range of Julia arrays.\n",
    "    prev = primelist[1] % m\n",
    "    class_counts[prev + 1] += 1\n",
    "    for i = 2:length(primelist)\n",
    "        next = primelist[i] % m\n",
    "        class_counts[next + 1] += 1\n",
    "        pair_counts[prev + 1, next + 1] += 1\n",
    "        prev = next\n",
    "    end\n",
    "    \n",
    "    # store the counts in the dictionary\n",
    "    d[:class_counts] = class_counts\n",
    "    d[:pair_counts] = pair_counts\n",
    "    \n",
    "    # results above include zero entries for impossible congruence\n",
    "    # classes; here we separately record a vector and a matrix without\n",
    "    # the zero entries\n",
    "    d[:nz_class_counts] = nonzero_class_counts(class_counts, d[:congruence_classes])\n",
    "    d[:nz_pair_counts] = nonzero_pair_counts(pair_counts, d[:congruence_classes])\n",
    "    \n",
    "    # normalize the results to allow comparisons of samples of different sizes\n",
    "    mc = mean(d[:nz_class_counts])\n",
    "    mp = mean(d[:nz_pair_counts])\n",
    "    d[:norm_class_counts] = map(x -> (x - mc) / mc, d[:class_counts])\n",
    "    d[:norm_pair_counts] = map(x -> (x - mp) / mp, d[:pair_counts])\n",
    "    d[:norm_nz_class_counts] = map(x -> (x - mc) / mc, d[:nz_class_counts])\n",
    "    d[:norm_nz_pair_counts] = map(x -> (x - mp) / mp, d[:nz_pair_counts])\n",
    "    \n",
    "    # elementary stats on the normalized results\n",
    "    d[:norm_class_counts_mean] = mean(d[:norm_nz_class_counts])\n",
    "    d[:norm_class_counts_std] = std(d[:norm_nz_class_counts])\n",
    "    d[:norm_pair_counts_mean] = mean(d[:norm_nz_pair_counts])\n",
    "    d[:norm_pair_counts_std] = std(d[:norm_nz_pair_counts])\n",
    "    d[:diag_ratio] = diagonal_ratio(d[:nz_pair_counts])\n",
    "    return d\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Graphics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Gadfly  # the Julia version of the ggplot2 graphics package\n",
    "\n",
    "using Colors  # for color keys and color maps in graphics\n",
    "\n",
    "using Cairo   # for graphics conversions, eg SVG --> PNG\n",
    "\n",
    "using DataFrames # 2D arrays with named columns; hard to use Gadfly without them\n",
    "\n",
    "Gadfly.set_default_plot_size(400pt, 400pt);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function colorfn(x)\n",
    "    if x == 0.0\n",
    "        return RGB(0.5, 0.5, 0.5)\n",
    "    else\n",
    "        index = round(Int, min(200, 1 + 200x))\n",
    "        return Colors.diverging_palette(245.0, 10.0, 200, wcolor=RGB(1,0,0), c=0.7)[index]\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function clamp_colors(mat)\n",
    "    max_value = maximum(mat)\n",
    "    if max_value > 1\n",
    "        adjusted_mat = similar(mat)\n",
    "        for i in eachindex(mat)\n",
    "            if mat[i] > 0\n",
    "                adjusted_mat[i] = mat[i] / max_value\n",
    "            else\n",
    "                adjusted_mat[i] = mat[i]\n",
    "            end\n",
    "        end\n",
    "        return adjusted_mat\n",
    "    end\n",
    "    return mat\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function heatmap(d::Dict)\n",
    "    mat = clamp_colors(d[:norm_nz_pair_counts])\n",
    "    labels = d[:congruence_classes]\n",
    "    spy(mat,\n",
    "        Scale.x_discrete(labels = x -> string(labels[x])),\n",
    "        Scale.y_discrete(labels = y -> string(labels[y])),\n",
    "        Guide.xlabel(\"j = second prime mod \" * string(d[:m])),\n",
    "        Guide.ylabel(\"i = first prime mod \" * string(d[:m]), orientation=:vertical),\n",
    "        Guide.title(\"normalized counts of consecutive \" * string(d[:digits]) * \"-digit primes mod \" * string(d[:m])),\n",
    "        Guide.colorkey(\"\"),\n",
    "        Scale.color_continuous(colormap = colorfn, minvalue = -1.0, maxvalue = 1.0))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function heatmap_z(d::Dict)\n",
    "    mat = clamp_colors(d[:norm_pair_counts])\n",
    "    labels = collect(0:d[:m] - 1)\n",
    "    spy(mat,\n",
    "        Scale.x_discrete(labels = x -> string(labels[x])),\n",
    "        Scale.y_discrete(labels = y -> string(labels[y])),\n",
    "        Guide.xlabel(\"j = second prime mod \" * string(d[:m])),\n",
    "        Guide.ylabel(\"i = first prime mod \" * string(d[:m]), orientation=:vertical),\n",
    "        Guide.title(\"normalized counts of consecutive \" * string(d[:digits]) * \"-digit primes mod \" * string(d[:m])),\n",
    "        Guide.colorkey(\"\"),\n",
    "        Scale.color_continuous(colormap = colorfn, minvalue = -1.0, maxvalue = 1.0))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# testmod7 = generate_primes(1000000, 100000)\n",
    "\n",
    "# test7data = analyze_primes_mod_m(testmod7, 7)\n",
    "\n",
    "# hm7 = heatmap(test7data)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
